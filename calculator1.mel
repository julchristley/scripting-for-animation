global proc float GetSum(float $input[])
{
    float $sum = 0;
    for ($i=0; $i <size($input); $i++)
    {
        $sum += $input[$i];
    }
    return $sum;
}

global proc float GetDifference(float $input[])
{
    float $difference = $input[0];
    for ($i = 1; $i <size($input); $i++)
    {
        $difference -=$input[$i];
    }
    return $difference;
}

global proc float GetProduct(float $input[])
{
    float $product = $input[0];
    for ($i=1; $i <size($input); $i++)
    {
        $product *= $input[$i];
    }
    return $product;
}

global proc float GetQuotient(float $input[])
{
    float $quotient = $input[0];
    for ($i=1; $i <size($input); $i++)
    {
        $quotient /=$input[$i];
    }
    return $quotient;
}

global proc float GetPower(float $input[])
{
    float $power = $input[0];
    for ($i=1; $i <size($input); $i++)
    {
        $power = pow($power, $input[$i]);
    }
    return $power;
}

global proc float GetMean(float $input[])
{
    float $mean =$input[0];
    for ($i=1; $i <size($input); $i++)
    {
        $mean +=$input[$i];
    }
    $mean = $mean/size($input);
    return $mean;
}

global proc float GetMedian(float $input[])
{
    int $length = size($input);
    float $temp[];
    for($i=0; $i<$length; $i++)
    {
        $temp[$i] = $input[$i];
    }
    for ($i=0; $i<$length-1; $i++)
    {
        for ($a=0; $a<$length-$i-1; $a++)
        {
            if ($temp[$a] > $temp[$a + 1])
            {
                float $swap = $temp[$a];
                $temp[$a] = $temp[$a + 1];
                $temp[$a + 1] = $swap;
            }
        }
    }
    if ($length % 2 == 1)
    {
        return $temp[$length/2];
    }
    else
    {
        int $even1 = ($length/2)-1;
        int $even2 = ($length/2);
        return ($temp[$even1]+$temp[$even2])/2;
    }
}


global proc string Calculator(string $op1, string $op2, float $input[])
{   
    float $result;
    string $resultStatement = "";

    for ($i = 0; $i < size($input); $i++)
    {
        $resultStatement += ("" + $input[$i]);

        if ($i < size($input) -1)
        {
            if ($op1 == "1")
            {
                $resultStatement += " + ";
            }
            else if ($op1 == "2")
            {
                $resultStatement += " - ";
            }
            else if ($op1 == "3")
            {
                $resultStatement += " * ";
            }
            else if ($op1 == "4")
            {
                $resultStatement += " / ";
            }
            else if ($op2 == "1")
            {
                $resultStatement += " ^ ";
            }
                else if ($op2 == "2")
            {
                $resultStatement += ", ";
            }
                else if ($op2 == "3")
            {
                $resultStatement += ", ";
            }
        }
    }


    if ($op1 == "1")
    {
        $result = GetSum($input);
    }
    else if ($op1 == "2")
    {
        $result = GetDifference($input);
    }
    else if ($op1 == "3")
    {
        $result = GetProduct($input);
    }
    else if ($op1 == "4")
    {
        $result = GetQuotient($input);
    }
    else if ($op2 == "1")
    {
        $result = GetPower($input);
    }
    else if ($op2 == "2")
    {
        $result = GetMean($input);
    }
    else if ($op2 == "3")
    {
        $result = GetMedian($input);
    }

    $resultStatement += " = " + string($result);
    return ($resultStatement + "\n");
}


global proc CalcButtonCmd()
{
    string $optionField1 = `optionVar -q "optionField1"`;
    string $optionField2 =`optionVar -q "optionField2"`;
    string $inputField = `optionVar -q "inputField"`;
    string $resultField = `optionVar -q "calcResult"`;



    string $inputT = `textField -q -text $inputField`;
    string $op1 = `radioButtonGrp -q -select $optionField1`;
    string $op2 = `radioButtonGrp -q -select $optionField2`;


    string $inputTemp[];

    tokenizeList($inputT, $inputTemp);

    float $input[];
    for ($str in $inputTemp)
    {
        int $index = size($input);
        $input[$index] = float($str);
    }

    string $result = Calculator($op1, $op2, $input);
    textField -e -text $result $resultField;
}

global proc CalcUI()
{
    string $mWindow = "placeWindow";
    if(`window -exists $mWindow`)
        deleteUI $mWindow;

    $mWindow = `window -title "Calculator"
                                -widthHeight 300 400
                                -resizeToFitChildren 1
                                $mWindow`;
    string $mColumn = `columnLayout -parent $mWindow -adjustableColumn 1`;
    optionVar -stringValue "inputField" `textField -parent $mColumn -placeholderText "Enter numbers here"`;
    string $buttonMerge = `radioButtonGrp -parent $mColumn -visible 0`;
    optionVar -stringValue "optionField1" `radioButtonGrp -parent $mColumn -numberOfRadioButtons 4 -shareCollection $buttonMerge -labelArray4 "Add" "Subtract" "Multiply" "Divide"`;
    optionVar -stringValue "optionField2" `radioButtonGrp -parent $mColumn -numberOfRadioButtons 3 -shareCollection $buttonMerge -labelArray3 "Power" "Mean" "Median"`;
    button -parent $mColumn -label "Run" -command CalcButtonCmd;
    optionVar -stringValue "calcResult" `textField -parent $mColumn -editable 0`;
    showWindow $mWindow;
}
CalcUI();